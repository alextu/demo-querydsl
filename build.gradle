buildscript {
    ext {
        springBootVersion = '1.2.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot' 

jar {
    baseName = 'demo-querydsl'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}


dependencies {
    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile 'org.hibernate:hibernate-validator:5.1.3.Final'
    compile 'javax.el:javax.el-api:3.0.0'
    compile 'org.springframework.data:spring-data-jdbc-core:1.1.0.RELEASE'
    compile 'com.mysema.querydsl:querydsl-sql:3.6.0'
    compile 'com.mysema.querydsl:querydsl-collections:3.6.0'
    compile("org.codehaus.groovy:groovy:2.4.0")
    runtime("com.h2database:h2")
    testCompile("org.springframework.boot:spring-boot-starter-test") 
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

configurations { querydsl }
dependencies {
    querydsl 'com.mysema.querydsl:querydsl-sql-codegen:3.6.0'
    querydsl 'org.slf4j:slf4j-log4j12:1.6.1'
    querydsl 'com.h2database:h2'
}

task generateQ {
    ext.destDir = new File('src/main/groovy')
    doLast {
        destDir.mkdirs()
        ant.taskdef(
                name: 'antMetaDataExporter',
                classname: 'com.mysema.query.sql.ant.AntMetaDataExporter',
                classpath: configurations.querydsl.asPath,
        )
        ant.antMetaDataExporter(
                dbUrl: 'jdbc:h2:~/test',
                dbUserName: 'sa',
                dbPassword: '',
                jdbcDriverClass: 'org.h2.Driver',
                targetPackage: 'io.alextizzle.demoquerydsl.support.q',
                targetSourceFolder: destDir,
                exportBeans: false,
        )
    }
}